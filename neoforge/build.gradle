plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = "Shedaniel"
        url = "https://maven.shedaniel.me/"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$architectury_api_version"

    modApi "me.shedaniel.cloth:cloth-config-neoforge:$cloth_config_version"

    forgeRuntimeLibrary "com.openai:openai-java:$openai_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand([
                mod_id: mod_id,
                version: version,
                neoforge_version_range: neoforge_version_range,
                minecraft_version_neoforge_range: minecraft_version_neoforge_range,
                architectury_api_version_neoforge_range: architectury_api_version_neoforge_range,
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
